buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'com.guardsquare:proguard-gradle:7.1.0'
  }
}

plugins {
  id 'java'
  id 'jacoco'
  id 'idea'
  id 'com.github.johnrengelman.shadow' version '6.1.0'
  id 'kr.entree.spigradle' version '2.2.4'
}

apply from: "$rootDir/gradle/publish.gradle"

group project.property("group")
version project.property("version")

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

compileJava {
  options.encoding = 'UTF-8'
  options.compilerArgs += ["-parameters"]
  options.fork = true
  options.forkOptions.executable = 'javac'
}

archivesBaseName = project.property("pluginName")

ext {
  mcVersion = project.property("mcVersion")
  libDir = "$rootDir/libs"
}

spigot {
  name = project.property("pluginName")
  authors = [project.property("author")]
  apiVersion = project.property("apiVersion")
  depends = ['DatabaseLib']
  softDepends = ['PlaceholderAPI']
  commands {
    bank {
      description = 'Root command for MultiCurrency'
      usage = '/bank <param>'
    }
  }
  permissions {
    'bank.*' {
      description = 'Gives access to all MultiCurrency commands'
      children = [
        'bank.give': true,
        'bank.giveall': true,
        'bank.take': true,
        'bank.pay': true,
        'bank.transfer': true,
        'bank.broadcast': true,
        'bank.set': true,
        'bank.reset': true,
        'bank.lead': true,
        'bank.reload': true,
      ]
    }
    'bank.give' {
      description = 'Gives access to the give command'
      defaults = 'op'
    }
    'bank.giveall' {
      description = 'Gives access to the giveall command'
      defaults = 'op'
    }
    'bank.take' {
      description = 'Gives access to the take command'
      defaults = 'op'
    }
    'bank.pay' {
      description = 'Gives access to the pay command'
      defaults = 'op'
    }
    'bank.transfer' {
      description = 'Gives access to the transfer command'
      defaults = 'op'
    }
    'bank.broadcast' {
      description = 'Gives access to the broadcast command'
      defaults = 'op'
    }
    'bank.set' {
      description = 'Gives access to the set command'
      defaults = 'op'
    }
    'bank.reset' {
      description = 'Gives access to the reset command'
      defaults = 'op'
    }
    'bank.lead' {
      description = 'Gives access to the lead command'
      defaults = 'op'
    }
    'bank.reload' {
      description = 'Gives access to the reload command'
      defaults = 'op'
    }
  }
}

repositories {
  mavenCentral()
  jcenter()
  jitpack()
  spigot()
  protocolLib()
  maven { url = 'https://repo.minecaftvn.com/releases' }
  maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
}

dependencies {
  implementation 'org.jetbrains:annotations:19.0.0'

  // Lombok anotation
  compileOnly lombok()
  // Spigot
  compileOnly spigot(mcVersion)
  
  // Dependency plugins here
  // DatabaseLib
  compileOnly 'me.khanghoang:DatabaseLib:0.1.2'
  // ProtocolLib
  compileOnly "com.comphenix.protocol:ProtocolLib:4.6.0"
  // PlaceholderAPI
  compileOnly 'me.clip:placeholderapi:2.10.9'
  // Vault API
  compileOnly "com.github.MilkBowl:VaultAPI:1.7"
}

processResources {
  outputs.upToDateWhen { false }
  filesMatching('**/plugin.yml') {
    filter {
      it.replace('@version@', project.version.toString())
    }
  }
}

task proguard(type: proguard.gradle.ProGuardTask) {
  configuration 'proguard.txt'

  injars "${jar.archiveFile}"
  outjars "${jar.archiveFile}-obf.jar"
}

defaultTasks 'build'
